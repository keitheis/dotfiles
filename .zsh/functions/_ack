#compdef ack

_arguments \
	"--noenv[Ignore environment variables and ~/.ackrc]" \
	"--man[Man page]" \
	"--help[This help]" \
	"--version[Display version & copyright]" \
	"--thpppt[Bill the Cat]" \
	'(--ignore-case -i)'{--ignore-case,-i}'[Ignore case distinctions in PATTERN]' \
	'(--smart-case --nosmart-case)'{--smart-case,--nosmart-case}'[Ignore case distinctions in PATTERN, only if PATTERN contains no upper case Ignored if -i is specified]' \
	'(--invert-match -v)'{--invert-match,-v}'[Invert match: select non-matching lines]' \
	'(--word-regexp -w)'{--word-regexp,-w}'[Force PATTERN to match only whole words]' \
	'(--literal -Q)'{--literal,-Q}'[Quote all metacharacters; PATTERN is literal]' \
	'(--all-types -a)'{--all-types,-a}'[All file types searched; Ignores CVS, .svn and other ignored directories]' \
	'(--recurse -R -r)'{--recurse,-R,-r}"[Recurse into subdirectories (ack's default behavior)]" \
	'(--no-recurse -n)'{--no-recurse,-n}'[No descending into subdirectories]' \
	'(--ignore-dir= --noignore-dir=)'{--ignore-dir=,--noignore-dir=}':Add/Remove directory from the list of ignored dirs:_dirs' \
	'--show-types[Show which types each file has.]' \
	'(--follow --nofollow)'{--follow,--nofollow}'[Follow symlinks. Default is off.]' \
	'--pager=:Pipes all ack output through COMMAND.:_command_names' \
	'--nopager[Do not send output through a pager.]' \
  '(--color-filename= --color-match= --color-lineno=)'{--color-filename=,--color-match=,--color-lineno=}':Set the color for filenames, matches, and line numbers.:_x_color' \
	'*:files:_files'
